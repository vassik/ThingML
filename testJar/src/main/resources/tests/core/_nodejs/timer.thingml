import "../timer.thingml"

// Manage a set of software timers.
thing TimerNodejs includes Timer
@pim "Timer"
@platform "javascript"
@javascript "node browser"
{
    function tcancel() 
    do
        'clearTimeout(this.timer);'
    end
    
    function tstart(delay : Integer)
    do
        'this.timer = setTimeout(() => {'
		timer!timer_timeout()
		'},' & delay & ');'
    end
    
    statechart SoftTimer init default {
        state default {
            
          internal
            event m : timer?timer_start
            guard m.delay > 0
            action do
                tstart(m.delay)
            end
            
          internal
            event m : timer?timer_cancel
            action tcancel()    
        }
    }
}
